*PROCESS ALIGN,COMPAT(MACROCASE,NOCASE),DXREF,FLAG(ALIGN,CONT,RECORD)
*PROCESS NOFOLD,NOINFO,PC(ON,DATA,GEN,MCALL)
*PROCESS RA2,NORLD,MXREF,RXREF,USING(MAP,WARN(9))
*WARNING - THIS PROGRAM REQUIRES THE HIGH-LEVEL ASSEMBLER
*          AS WELL AS LE/370
*          THIS PROGRAM IS RE-ENTRANT.
         PUSH  PRINT
         PRINT NOGEN
         IEABRCX DEFINE
         IEABRCX ENABLE
_BALR    OPSYN BALR
BALR     OPSYN BASR
         POP   PRINT
         YREGS
         GBLC  &DLL
&DLL     SETC  'YES'
         SYSSTATE ASCENV=P,
               AMODE64=NO,
               ARCHLVL=2
ISGQUERY CEEENTRY PPA=ISGQUERY_PPA,
               MAIN=NO,
               EXPORT=&DLL,
               AUTO=DSASIZE,
               BASE=R11
         USING CEECAA,R12
         USING CEEDSA,R13
*        DROP  R11
         J     GO
GOBACK   DS    0H
         CEETERM RC=RETURN_CODE,
               MODIFIER=MODIFIER
GO       DS    0H
         LARL  R10,LTORG
         USING LTORG,R10
         MVC   RETURN_CODE,=F'20' ERROR
         MVC   MODIFIER,=F'1'     NO PARMS
         LTR   R9,R1
         JZ    GOBACK
         MVC   MODIFIER,=F'2'     TOO FEW PARMS
         LT    R8,0(,R9)          POINTER TO QNAME
         JNP   GOBACK
         MVC   MODIFIER,=F'2'     TOO FEW PARMS
         LT    R7,4(,R9)          POINTER TO RNAME
         JNP   GOBACK
         MVC   MODIFIER,=F'2'     TOO FEW PARMS
         LT    R6,8(,R9)          POINTER TO RNAME LENGTH
         JNP   GOBACK
         MVC   MODIFIER,=F'2'     TOO FEW PARMS
         LT    R5,12(,R9)         POINTER TO ISGQUERY AREA
         JNP   GOBACK
         MVC   MODIFIER,=F'3'     TOO MANY PARMS
         LT    R4,16(,R9)         POINTER TO POINTER TO RETURN AREA
         JNM   GOBACK
         L     R4,0(,R4)          DEREFERENCE POINTER
         ST    R4,@RETURN
         A     R4,0(,R4)          ADD IN STORAGE_LENGTH
         ST    R4,@END            SAVE
         USING GQDSECT,R5
         LA    R2,RNAMELEN
         ISGQUERY REQINFO=QSCAN,
               RETCODE=RETURN_CODE,
               RSNCODE=MODIFIER,
               ANSAREA=ISGANS,
               ANSDETAIL=FULL2,
               ANSLEN=ALISGANS,
               QNAME=(8),
               QNAMEMATCH=PATTERN, SPECIFIC
               SCANACTION=START, RESUME, QUIT
               RNAMEMATCH=PATTERN, SPECIFIC
               RNAME=(7),
               RNAMELEN=(6),
               RESUMETOKEN=TOKEN,
               SCOPE=ANY,
               SEARCH=BY_FILTER,
               MF=(E,(5),COMPLETE)
*              GATHERFROM=SYSPLEX,
         L     R3,@RETURN
         LA    R3,8(,R3)          GO PAST HEADER
LOOP0    DS    0H
         ST    R15,QUERY_RC
         CHI   R15,8
         JNL   GOBACK
         LTR   R15,R15
         JZ    NOWARN
*
* WARNING ISSUED. SEE IF I WANT TO HANDLE IT
         LLH   R0,MODIFIER+2
         CHI   R0,X'0405'         ANSWER AREA FULL
         JE    NOWARN
QUERY_QUIT DS  0H
         ISGQUERY REQINFO=QSCAN,
               SCANACTION=QUIT,
               RESUMETOKEN=TOKEN,
               RETCODE=RETURN_CODX,
               RSNCODE=MODIFIEX,
               MF=(E,(5))
         J     GOBACK
NOWARN   DS    0H
         L     R5,12(,R9)         POINTER TO ISGQUERY AREA
         LA    R4,ISGANS
         USING ISGYQUAAHDR,R4
         L     R5,ISGYQUAAHDRFIRSTRECORD31
         USING ISGYQUAARS,R5
LOOP1    DS    0H                 QNAME LOOP
*        L     R7,ISGYQUAARSRNAME31                       &RNAME
         LLH   R8,ISGYQUAARSRNAMELEN
         L     R6,ISGYQUAARSFIRSTRQ31 &REQUEST DATA
         USING ISGYQUAARQ,R6
LOOP2    DS    0H                 RNAME LOOP
         L     R7,ISGYQUAARQRQX31 &REQUESTOR EXTENTION
         USING ISGYQUAARQX,R7
* PROCESS REQUEST DATA
         USING RETURN_STRUC,R3
         ST    R8,RTN_RLEN
         L     R2,ISGYQUAARSRNAME31
         LR    R1,R8
         BCTR  R1,0
         EX    R1,CP_RNAME
         TM    ISGYQUAARQFLAGS1,ISGYQUAARQCONTROL
         JO    SHARED
         MVC   RTN_EXCSHR,=CL8'EXCL'
         J     CONT1
CP_RNAME MVC   RTN_RNAME(0),0(R2)
SHARED   DS    0H
         MVC   RTN_EXCSHR,=CL8'SHARED'
CONT1    DS    0H
         MVC   RTN_QNAME,ISGYQUAARSQNAME
         MVC   RTN_JOBNAME,ISGYQUAARQXJOBNAME
         MVC   RTN_SYSNAME,ISGYQUAARQXSYSNAME
         MVC   RTN_SCOPE,=CL8'STEP'
         CLI   ISGYQUAARSSCOPE,ISGYQUAA_KSTEP
         JE    CONT3
         MVC   RTN_SCOPE,=CL8'SYSTEM'
         CLI   ISGYQUAARSSCOPE,ISGYQUAA_KSYSTEM
         JE    CONT3
         MVC   RTN_SCOPE,=CL8'SYSPLEX'
         CLI   ISGYQUAARSSCOPE,ISGYQUAA_KSYSPLEX
         JE    CONT3
         MVC   RTN_SCOPE+1(7),=CL7'ERROR'
         MVC   RTN_SCOPE(1),ISGYQUAARSSCOPE
CONT3    DS    0H
         TM    ISGYQUAARQFLAGS1,ISGYQUAARQOWNER
         JO    OWNER
         TM    ISGYQUAARQFLAGS1,ISGYQUAARQMATU
         JO    OWNER
         MVC   RTN_STATUS,=CL8'WAIT'
         J     CONT2
OWNER    DS    0H
         MVC   RTN_STATUS,=CL8'OWNED'
CONT2    DS    0H
         MVC   RTN_ASID,ISGYQUAARQXASID
         MVC   RTN_TCB,ISGYQUAARQTCB
         MVC   RETURN_CODE,=F'24'
         MVC   MODIFIER,=F'1'
         LA    R3,RTN_NEXT
         CL    R3,@END
         JNL   QUERY_QUIT
         DROP  R7
         LT    R6,ISGYQUAARQNEXT31
         JNZ   LOOP2              GOT ANOTHER IF != NULL
         DROP  R6
         LT    R5,ISGYQUAARSNEXT31 NEXT QNAME
         JNZ   LOOP1              LOOP IF != NULL
         DROP  R5
         L     R4,@RETURN
         ST    R3,4(,R4)          STORE END ADDRESS
         XC    RETURN_CODE,RETURN_CODE
         XC    MODIFIER,MODIFIER
         LT    R15,QUERY_RC
         JZ    GOBACK
         L     R5,12(,R9)         POINTER TO ISGQUERY AREA
         ISGQUERY REQINFO=QSCAN,
               SCANACTION=RESUME,
               ANSAREA=ISGANS,
               ANSLEN=ALISGANS,
               RESUMETOKEN=TOKEN,
               RETCODE=RETURN_CODE,
               RSNCODE=MODIFIER,
               MF=(E,(5))
         J     LOOP0
LTORG    DS    0D
ALISGANS DC    A(LISGANS)
*TITLE    DC    CL80'ISGQUERY DUMP'
*OPTIONS  DC    CL255'BLOCKS,STORAGE,REGST(256),GENOPTS'
         LTORG *
ISGQUERY_PPA CEEPPA LIBRARY=NO,
               PPA2=YES,
               EXTPROC=YES,
               TSTAMP=YES,
               PEP=YES,
               INSTOP=YES,
               EXITDSA=NO,
               OWNEXM=YES,
               EPNAME=ISGQUERY,
               VER=1,
               REL=1,
               MOD=0,
               DSA=YES
         CEEDSA
* DYNAMIC AREA IS DEFINED HERE.
* THIS IS WITHIN A DSECT, SO NO DATA IS REALLY INITIALIZED
QUERY_RC DS    F
@RETURN  DS    A
@END     DS    A
RNAMELEN DS    F
TOKEN    DS    F
SYS      DS    F
* BIT MEANING
*    0
*    CURRENTLY EXECUTING IN THE CICS ENVIRONMENT
*    1
*    CURRENTLY EXECUTING IN A CICS_PIPI ENVIRONMENT
*    2-3
*    RESERVED FOR OTHER SPECIFIC CICS ENVIRONMENTS
*    4
*    CURRENTLY EXECUTING IN A TSO ENVIRONMENT
*    5
*    CURRENTLY EXECUTING IN A BATCH ENVIRONMENT
*    6
*    CURRENTLY EXECUTING IN A Z/OS UNIX ENVIRONMENT
*    7-28
*    RESERVED FOR FUTURE USE
*    29
*    CURRENTLY EXECUTING ON Z/VSE(TM)
*    30
*    CURRENTLY EXECUTING ON Z/OS
*    31
*    PREVIOUSLY INDICATED AS EXECUTING ON Z/OS.E
*
ENV      DS    F
*
MEMBER   DS    F
*
GPID     DS    F
FC       DS    3F
RETURN_CODE DS F
MODIFIER DS    F
RETURN_CODX DS F
MODIFIEX DS    F
CALLX    DS    30A
ISGANS   DS    0D
         DS    20000F
LISGANS  EQU   *-ISGANS
DSASIZE  EQU   *-CEEDSA
         BPXYERNO LIST=YES
         CEECAA
         ISGYQUAA
         ISGYCON
         ISGRNLE
GQDSECT  DSECT
         ISGQUERY MF=(L,GQSCAN)
RETURN_STRUC DSECT
RTN_QNAME DS   CL8                ISGYQUAARSQNAME
RTN_JOBNAME DS CL8                ISGYQUAARQXJOBNAME
RTN_RLEN DS    F                  ISGYQUAARSRNAMELEN
RTN_SYSNAME DS CL8                ISGYQUAARQXSYSNAME
RTN_EXCSHR DS  CL8                ISGYQUAARQFLAGS1
*ISGYQUAARQCONTROL EQ X'80'       ON-SHARED
*ISGYQUAARQOWNER   EQ X'08'
*ISGYQUAARQMATU    EQ X'04'
RTN_STATUS DS  CL8
RTN_SCOPE  DS  CL8                ISGYQUAARSSCOPE
* ISGYQUAA_KSTEP
* ISGYQUAA_KSYSTEM
* ISGYQUAA_KSYSPLEX
RTN_TCB  DS    A
RTN_ASID DS    H
RTN_RNAME DS   CL255
RTN_NEXT EQU   *
         END   ISGQUERY
